plugins {
    id "io.spring.dependency-management"
    id "java-library"
    id "maven-publish"
}

apply from: "gradle/libraries.gradle"
apply from: "gradle/repositories.gradle"

version = "3.4.0"
group = "ru.b.q"
compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
}

dependencyManagement {
    imports {
        mavenBom("ru.b.q:q-dependencies:${qDependenciesVersion}")
    }
}

dependencies {
    implementation "org.springframework.boot:spring-boot"

    compileOnly "org.projectlombok:lombok"

    annotationProcessor "org.projectlombok:lombok"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    testImplementation "org.springframework.boot:spring-boot-starter-test"

    testCompileOnly "org.projectlombok:lombok"

    testAnnotationProcessor "org.projectlombok:lombok"
}

configurations.configureEach {
    exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
}

tasks.named("compileJava") {
    inputs.files(tasks.named("processResources"))
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            versionMapping {
                usage("java-api") {
                    fromResolutionOf("runtimeClasspath")
                }
                usage("java-runtime") {
                    fromResolutionResult()
                }
            }
        }
    }

    repositories {
        mavenLocal()
    }
}
